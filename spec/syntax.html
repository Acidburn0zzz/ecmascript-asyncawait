<h2>Syntax</h2>
<emu-grammar>
    AsyncFunctionDeclaration[Yield, Await, Default] ::
        `async` [no LineTerminator here] `function` BindingIdentifier[?Yield, ?Await] `(` FormalParameters[Await] `)` `{` AsyncFunctionBody `}` #1
        [+Default] `async` [no LineTerminator here] `function` `(` FormalParameters[Await] `)` `{` AsyncFunctionBody `}`                        #2

    AsyncFunctionExpression ::
        `async` [no LineTerminator here] `function` BindingIdentifier? `(` FormalParameters[Await] `)` `{` AsyncFunctionBody `}` #1

    AsyncMethod ::
        `async` [no LineTerminator here] PropertyName `(` StrictFormalParameters[Await] `)` `{` AsyncFunctionBody `}` #1

    AsyncFunctionBody ::
        FunctionBody[Await] #1

    AwaitExpression[Yield] ::
        `await` UnaryExpression[?Yield, Await] #1

    AsyncArrowFunction[In, Yield] ::
        `async` [no LineTerminator here] AsyncArrowBindingIdentifier[?Yield] [no LineTerminator here] `=>` ConciseBody[?In, Await] #1
        CoverCallExpressionAndAsyncArrowHead[?Yield, Await] [no LineTerminator here] `=>` ConciseBody[?In, Await]                  #2

    AsyncArrowBindingIdentifier[Yield]
        BindingIdentifier[?Yield, Await] #1

    CoverCallExpressionAndAsyncArrowHead[Yield, Await] ::
        MemberExpression[?Yield, ?Await] Arguments[?Yield, ?Await] #1
</emu-grammar>

<emu-note>`yield` is |Identifier| or |YieldExpression| in |FunctionParameters| of |AsyncFunctionDeclaration| and |AsyncFunctionExpression| and |ArrowParameters| of |AsyncArrowFunction| depending on context. `await` is always parsed as an |AwaitExpression| but is a Syntax Error via static semantics. Yield is always allowed as an identifier of an |AsyncFunctionExpression|.</emu-note>


<h3>Supplemental Syntax</h3>
<p>When processing the production <emu-prodref name="AsyncArrowFunction" a="1" class="inline"></emu-prodref> the interpretation of |CoverCallExpressionAndAsyncArrowHead| is refined using the following gramar:</p>

<emu-grammar>
    AsyncArrowHead[?Yield] ::
        async [no LineTerminator here] ArrowFormalParameters[?Yield, Await] #1
</emu-grammar>
